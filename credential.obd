prompt Creating type body TOracleJobCredential ...
create or replace type body TOracleJobCredential is 
/************************************************************************
 * Тело типа - описания учетной записи пользователя ОС под которым будет*
 * выполняться внешнее задание PL/Job                                   *
 *                                                                      *                             
 * @file    credential.obd                                              *
 * @author  Evgeny Sergeev                                              *
 * @version 1.0.0                                                       *
 * @history                                                             *
 * Evgeny Sergeev 01.04.2017 - created                                  *
 ************************************************************************
 * Copyright (c) 2017 Evgeny Sergeev                                    *
 * email: Bazinko@yandex.ru                                             *
 ***********************************************************************/  
  
  constructor function TOracleJobCredential(v_pName          in varchar2,      
                                            v_pUserName      in varchar2,      
                                            v_pPassword      in varchar2, 
                                            v_pDatabaseRole  in varchar2 := null,     
                                            v_pWindowsDomain in varchar2 := null,
                                            v_pComments      in varchar2 := null)  return self as result is
  begin
    self.FName          := v_pName;             
    self.FUsername      := v_pUserName;     
    self.FPassword      := v_pPassword;     
    self.FDatabaseRole  := v_pDatabaseRole; 
    self.FWindowsDomain := v_pWindowsDomain;            
    self.FComment       := v_pComments;         

    return;
  end;


  constructor function TOracleJobCredential(v_pName in varchar2)  return self as result is
    v_xCredential user_scheduler_credentials%rowtype;
  begin
    select
      *
    into
      v_xCredential
    from
      user_scheduler_credentials uc
    where
      uc.credential_name=upper(trim(v_pName));

    self.FName          := v_xCredential.Credential_name;
    self.FUsername      := v_xCredential.Username;
    self.FDatabaseRole  := v_xCredential.Database_Role;
    self.FWindowsDomain := v_xCredential.Windows_Domain;
    self.FComment       := v_xCredential.Comments;

    return;
  end;


  overriding member procedure save(self in out nocopy TOracleJobCredential) is
  begin
    (self as TPersistent).save;

    dbms_scheduler.create_credential(credential_name => self.FName,    
                                     username        => self.FUsername,
                                     password        => self.FPassword,
                                     database_role   => self.FDatabaseRole, 
                                     windows_domain  => self.FWindowsDomain,
                                     comments        => self.FComment);
    commit;
  end;


  overriding member procedure remove(self in out nocopy TOracleJobCredential) is
  begin
    dbms_scheduler.drop_credential(credential_name => self.FName);

    (self as TPersistent).remove;

    commit;
  end;

end;
/
show errors;

prompt Done
